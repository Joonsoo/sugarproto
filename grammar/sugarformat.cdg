Items = WS2 (IndentItem WS1 Comment? EOL $0)*
IndentItem = WS1 Item {IndentItem(indent=str($0), item=$1)}

Item: Item = SingleItem | ListItem

SingleItem = ItemPath WS1 ItemValue {SingleItem(key=$0, value=$2)}
ListItem: ListItem = ListValueItem | ListFieldItem
ListValueItem = '-' WS1 Value {ListValueItem(value=$2)}
ListFieldItem = '-' WS1 ItemPath WS1 ItemValue {ListFieldItem(innerIndent=str($0, $1), key=$2, value=$4)}


ItemPath = KeyValue (WS1 '.' WS1 KeyValue)* {ItemPath(path=[$0] + $1)}

ItemValue: ItemValue = ":" {Header()}
  | ':' WS1 Value

Value: Value = ScalarValue
  | RepeatedValue
  | ObjectOrMapValue

ScalarValue: ScalarValue = StringValue
  | NumberValue
  | NameValue
  | DurationValue
  | TimestampValue

KeyValue: KeyValue = Name {NameKey(name=$0)} | StringFrac | NumberValue

Name = 'a-zA-Z_' 'a-zA-Z_0-9'* {str($0, $1)}

RepeatedValue = '[' WS2 ']' {RepeatedValue(elems=[])}
  | '[' WS2 Value (WS2 ',' WS2 Value)* (WS2 ',')? WS2 ']'
    {RepeatedValue([$2] + $3)}

ObjectOrMapValue = '{' WS2 '}' {ObjectOrMapValue(pairs=[])}
  | '{' WS2 KeyValuePair ((WS2 ',')? WS2 KeyValuePair)* WS2 '}'
    {ObjectOrMapValue([$2] + $3)}
KeyValuePair = ItemPath WS2 ':' WS2 Value {KeyValuePair(key=$0, value=$4)}

StringValue = StringFrac (WS2 StringFrac)* {StringValue(type=null, fracs=[$0]+$1)}
  | StringTypeAnnot StringFrac {StringValue(type=$0, fracs=[$1])}
StringFrac = '"' StringElem* '"' {StringFrac(elems=$1)}
StringElem: StringElem = '\\' 'abfnrtv?\\\'"' {EscapeCode(code=$1)}
  | '\\' oct (oct oct?)? {OctCode(code=str($1, $2))}
  | '\\' 'xX' hex hex? {HexCode(code=str($2, $3))}
  | '\\' 'u' hex hex hex hex {Unicode(code=str($2, $3, $4, $5))}
  | '\\' 'U' '0' '0' '0' hex hex hex hex hex {Unicode(code=str($2, $3, $4, $5, $6, $7, $8, $9))}
  | '\\' 'U' '0' '0' '1' '0' hex hex hex hex {Unicode(code=str($2, $3, $4, $5, $6, $7, $8, $9))}
  | <(.-'"\\')+> {PlainText(value=str($0))}
oct = '0-8'
hex = '0-9A-Fa-f'
StringTypeAnnot: %StringTypeAnnot = 'b' {%Base64} | 'h' {%Hex}
NumberValue: NumberValue = DecValue | OctValue | HexValue
DecValue = '+\-'? Dec ('.' Dec)? ('eE' '+\-'? Dec {Exponent(sgn=$1, value=$2)})?
           {DecValue(sgn=$0, integral=$1, frac=$2, exponent=$3)}
OctValue = '+\-'? '0' oct+ {OctValue(sgn=$0, value=str($2))}
HexValue = '+\-'? '0' 'xX' hex+ {HexValue(sgn=$0, value=str($3))}
Dec = '0' {"0"} | '1-9' '0-9'* {str($0, $1)}

NameValue = Name {NameValue(value=$0)}

DurationValue = (Days WS1)? (Hours WS1)? (Minutes WS1)? Seconds
                {DurationValue(days=$0$0, hours=$1$0, minutes=$2$0, seconds=$3)}
  | (Days WS1)? (Hours WS1)? Minutes {DurationValue($0$0, $1$0, $2, null)}
  | (Days WS1)? Hours {DurationValue($0$0, $1, null, null)}
  | Days {DurationValue($0, null, null, null)}
Days = Number WS1 'd' $0
Hours = Number WS1 'h' $0
Minutes = Number WS1 'm' $0
Seconds = Number SecondFrac? WS1 's' {Seconds(integral=$0, frac=$1)}
SecondFrac = '.' '0-9'+ {str($1)}
Number = '0' {"0"} | '1-9' '0-9'* {str($0, $1)}

TimestampValue = Date ('T' Time)? Timezone? {TimestampValue(date=$0, time=$1, timezone=$2)}
Date = digit4 '-' digit2 '-' digit2 {Date(year=$0, month=$2, day=$4)}
Time = digit2 ':' digit2 ':' digit2 SecondFrac?
       {Time(hour=$0, minute=$2, second=$4, secondFrac=$5)}
  | digit2 digit2 digit2 SecondFrac? {Time(hour=$0, minute=$1, second=$2, secondFrac=$3)}
  | digit2 ':' digit2 {Time($0, $2, null, null)}
  | digit2 digit2 {Time($0, $1, null, null)}
digit4 = '0-9' '0-9' '0-9' '0-9' {str($0, $1, $2, $3)}
digit2 = '0-9' '0-9' {str($0, $1)}
Timezone: Timezone = 'Z' {NamedTimezone(name="UTC")}
  | '+\-' digit2 {TimeOffset(hour=$1, minute="00")}
  | '+\-' digit2 ':' digit2 {TimeOffset(hour=$1, minute=$3)}
  | '+\-' digit2 digit2 {TimeOffset(hour=$1, minute=$2)}

WS1 = ' \t'*
NL = '\n\r'
WS2 = ' \t'* (NL EmptyLine* ' \t'*)?
EOL = EOF | NL EmptyLine*
EmptyLine = WS1 Comment? NL
Comment = '#' (.-NL)*
EOF = !.
