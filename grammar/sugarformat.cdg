Items = WS2 (Item WS1 Comment? EOL $0)*
Item: Item = SingleItem | ListValueItem | ListFieldItem | MapItem

SingleItem = WS1 Name WS1 ItemValue {SingleItem(indent=str($0), key=$1, value=$3)}
ListValueItem = WS1 '-' WS1 Value {ListValueItem(indent=str($0), value=$3)}
ListFieldItem = WS1 '-' WS1 Name WS1 ItemValue {ListFieldItem(indent=str($0), name=$3, value=$5)}
MapItem = WS1 KeyValue WS1 ItemValue


ItemValue: ItemValue = "::" {ObjectHeader()}
  | "[]" {RepeatedHeader()}
  | "{}" {MapHeader()}
  | ':' WS1 Value

Value: Value = StringValue | NumberValue | EnumValue | RepeatedValue | ObjectValue

KeyValue: KeyValue = StringFrac | NumberValue

Name = 'a-zA-Z_' 'a-zA-Z_0-9'* {str($0, $1)}

RepeatedValue = '[' WS2 ']' {RepeatedValue(elems=[])}
  | '[' WS2 Value (WS2 ',' WS2 Value)* (WS2 ',')? WS2 ']'
    {RepeatedValue([$2] + $3)}

ObjectValue = '{' WS2 '}' {ObjectValue(pairs=[])}
  | '{' WS2 KeyValuePair ((WS2 ',')? WS2 KeyValuePair)* WS2 '}'
    {ObjectValue([$2] + $3)}
KeyValuePair = Key WS2 ':' WS2 Value {KeyValuePair(key=$0, value=$4)}
Key: Key = Name {NameKey(name=$0)} | KeyValue

StringValue = StringFrac (WS2 StringFrac)* {StringValue(type=null, fracs=[$0]+$1)}
  | StringTypeAnnot StringFrac {StringValue(type=$0, fracs=[$1])}
StringFrac = '"' StringElem* '"' {StringFrac(elems=$1)}
StringElem: StringElem = '\\' 'abfnrtv?\\\'"' {EscapeCode(code=$1)}
  | '\\' oct (oct oct?)? {OctCode(code=str($1, $2))}
  | '\\' 'xX' hex hex? {HexCode(code=str($2, $3))}
  | '\\' 'u' hex hex hex hex {Unicode(code=str($2, $3, $4, $5))}
  | '\\' 'U' '0' '0' '0' hex hex hex hex hex {Unicode(code=str($2, $3, $4, $5, $6, $7, $8, $9))}
  | '\\' 'U' '0' '0' '1' '0' hex hex hex hex {Unicode(code=str($2, $3, $4, $5, $6, $7, $8, $9))}
  | <(.-'"\\')+> {PlainText(value=str($0))}
oct = '0-8'
hex = '0-9A-Fa-f'
StringTypeAnnot: %StringTypeAnnot = 'b' {%Base64} | 'h' {%Hex} | 't' {%Timestamp} | 'd' {%Duration}
NumberValue: NumberValue = DecValue | OctValue | HexValue
DecValue = '+\-'? Dec ('.' Dec)? ('eE' '+\-'? Dec)?
           {DecValue(sgn=$0, integral=$1, frac=$2, exponent=$3)}
OctValue = '+\-'? '0' oct+ {OctValue(sgn=$0, value=str($2))}
HexValue = '+\-'? '0' 'xX' hex+ {HexValue(sgn=$0, value=str($3))}
Dec = '0' {"0"} | '1-9' '0-9'* {str($0, $1)}

EnumValue = Name {EnumValue(value=$0)}

WS1 = ' \t'*
NL = '\n\r'
WS2 = ' \t\n\r'*
EOL = EOF | NL EmptyLine*
EmptyLine = WS1 Comment? NL
Comment = '#' (.-NL)*
EOF = !.
